// *******************************************************
//  esp32fileServer          by NoRi 2025-08-01
// -------------------------------------------------------
// SD_handler.cpp
// *******************************************************
#include "fileServer.h"
// -------------------------------------------------------
void SD_flServerSetup();
void SD_Dir(AsyncWebServerRequest *request);
void SD_Directory();
void SD_UploadFileSelect();
void SD_handleFileUpload(AsyncWebServerRequest *request, const String &filename, size_t index, uint8_t *data, size_t len, bool final);
void SD_Handle_File_Delete(String encoded_filename);
void SD_Generate_Confirm_Page(String encoded_filename);
void SD_File_Rename();
void SD_Handle_File_Rename(AsyncWebServerRequest *request, String filename, int Args);
bool SD_notFound(AsyncWebServerRequest *request);
void SD_Select_File_For_Function(String title, String function);
void SD_Select_File_For_ViewText();
void SD_View_Text(AsyncWebServerRequest *request, String encoded_filename);
// -------------------------------------------------------
void SDdir_flserverSetup();
void SDdir_handle_chTop();
void SDdir_handle_chUp();
void SDdir_Select_Dir_For_Function(String title, String function);
void SDdir_Generate_Confirm_Page(String encoded_filename);
void SDdir_Handle_chdir(String filename);
void SDdir_Handle_rmdir(String encoded_filename);
void SDdir_Handle_mkdir(AsyncWebServerRequest *request);
void SDdir_DirMake();
void SDdir_DirList();
void SDdir_FilesList();
bool SDdir_notFound(AsyncWebServerRequest *request);
void SDdir_InputNewDirName(String Heading, String Command, String Arg_name);
// -------------------------------------------------------
extern AsyncWebServer server;
extern String webpage;
std::vector<fileinfo> SD_Filenames;
uint32_t SD_startTime, SD_downloadTime = 1, SD_uploadTime = 1;
uint64_t SD_downloadSize, SD_uploadSize;
uint32_t SD_numfiles;
String SdPath = "/";

void SD_flServerSetup()
{
  // Serial.println(__FILE__);

  server.on("/SD_download", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_Downloading file...");
    SD_Select_File_For_Function("[DOWNLOAD] for PC", "SD_downloadhandler");
    request->send(200, "text/html", webpage); });

  server.on("/SD_upload", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_Uploading file...");
    SD_UploadFileSelect();
    request->send(200, "text/html", webpage); });

  server.on("/SD_handleupload", HTTP_POST, [](AsyncWebServerRequest *request) {}, [](AsyncWebServerRequest *request, const String &filename, size_t index, uint8_t *data, size_t len, bool final)
            { SD_handleFileUpload(request, filename, index, data, len, final); });

  // ********************** SD text file viewer ****************
  server.on("/SD_view_text", HTTP_GET, [](AsyncWebServerRequest *request)
            {
              String filename_encoded = "";
              if (request->hasParam("file"))
              { // „Éë„É©„É°„Éº„ÇøÂêç„Çí 'file' „Å®„Åô„ÇãÂ†¥Âêà
                filename_encoded = request->arg("file");
              }
              else
              {
                // „Ç®„É©„ÉºÂá¶ÁêÜ or notFound „Å∏
                request->send(400, "text/plain", "Missing file parameter");
                return;
              }
              Serial.println("SD_View_Text requested for: " + filename_encoded);
              SD_View_Text(request, filename_encoded);
              // SD_View_Text ÂÜÖ„Åß request->send „Åô„Çã„ÅÆ„Åß„Åì„Åì„Åß„ÅØ‰∏çË¶Å
            });

  server.on("/SD_vTxt", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_vTxt: text file viewer...");
    SD_Select_File_For_ViewText();
    request->send(200, "text/html", webpage); });
  // *********************************************************************

  server.on("/SD_stream", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_Streaming file...");
    SD_Select_File_For_Function("[STREAM]", "SD_streamhandler");
    request->send(200, "text/html", webpage); });

  server.on("/SD_rename", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_Renaming file...");
    SD_File_Rename();
    request->send(200, "text/html", webpage); });

  server.on("/SD_dir", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_File Directory...");
    SD_Dir(request);
    request->send(200, "text/html", webpage); });

  server.on("/SD_delete", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SD_Deleting file...");
    SD_Select_File_For_Function("[DELETE]", "SD_deletehandler");
    request->send(200, "text/html", webpage); });
}

void SD_Dir(AsyncWebServerRequest *request)
{
  SD_Directory();
  webpage = HTML_Header();
  webpage += "<h3>SD: Content (" + SdPath + ")</h3>";
  if (SD_numfiles > 0)
  {
    webpage += "<table class='file-list-table'>";
    webpage += "<tbody>";
    for (int index = 0; index < SD_numfiles; index++)
    {
      webpage += "<tr class='file-entry'>";
      // NameÂàó: Dir„ÅÆÂ†¥Âêà„ÅØ„Ç¢„Ç§„Ç≥„É≥„ÇíËøΩÂä†
      webpage += "<td class='file-name'>";
      if (SD_Filenames[index].ftype == "Dir")
      {
        webpage += "<span style='color: #007bff;'>&#128193;</span> "; // „Éï„Ç©„É´„ÉÄ„Ç¢„Ç§„Ç≥„É≥ üìÅ
      }
      webpage += SD_Filenames[index].filename;
      webpage += "</td>";
      // SizeÂàó: Â§âÊõ¥„Å™„Åó
      webpage += "<td class='file-size'>" + SD_Filenames[index].fsize + "</td>";
      webpage += "</tr>";
    }
    webpage += "</tbody>";
    webpage += "</table>";
  }
  else
  {
    webpage += "<p style='text-align: center; margin-top: 20px;'>No files or directories found in " + SdPath + "</p>";
  }
  webpage += HTML_Footer();
}

const String SD_SYSTEM_FILE = "System Volume Information";
void SD_Directory()
{
  SD_numfiles = 0;
  SD_Filenames.clear();
  if (SdPath == "")
    SdPath = "/";
  Serial.println("SdPath = " + SdPath);
  File root = SD.open(SdPath, "r");

  if (root)
  {
    root.rewindDirectory();
    File file = root.openNextFile();

    while (file)
    {
      String tmp_filename = (String(file.name()).startsWith("/") ? String(file.name()).substring(1) : file.name());

      // „Éë„Çπ„Åã„Çâ„Éï„Ç°„Ç§„É´ÂêçÈÉ®ÂàÜ„Å†„Åë„ÇíÊäΩÂá∫ (file.name()„Åå„Éï„É´„Éë„Çπ„ÇíËøî„ÅôÂ†¥Âêà„Åå„ÅÇ„Çã„Åü„ÇÅ)
      int lastSlash = tmp_filename.lastIndexOf('/');
      if (lastSlash != -1)
      {
        tmp_filename = tmp_filename.substring(lastSlash + 1);
      }

      if (tmp_filename != SD_SYSTEM_FILE && tmp_filename != "") // Á©∫„ÅÆ„Éï„Ç°„Ç§„É´Âêç„ÇÇÈô§Â§ñ
      {
        fileinfo tmp;
        tmp.filename = tmp_filename;
        tmp.ftype = (file.isDirectory() ? "Dir" : "File");
        if (tmp.ftype == "File")
          tmp.fsize = ConvBytesUnits(file.size(), 1);
        else
          tmp.fsize = "";

        SD_Filenames.push_back(tmp);
        SD_numfiles++;
      }
      file = root.openNextFile();
    }
    root.close();
  }
  std::sort(SD_Filenames.begin(), SD_Filenames.end(), compareFileinfo);
}

void SD_UploadFileSelect()
{
  webpage = HTML_Header();
  webpage += "<h3>SD: Select a File to [UPLOAD] to this device (" + SdPath + ")</h3>";
  webpage += "<form method = 'POST' action = '/SD_handleupload' enctype='multipart/form-data'>";
  webpage += "<input type='file' name='filename'><br><br>";
  webpage += "<input type='submit' value='Upload'>";
  webpage += "</form>";
  webpage += HTML_Footer();
}

void SD_handleFileUpload(AsyncWebServerRequest *request, const String &filename, size_t index, uint8_t *data, size_t len, bool final)
{
  String file = filename;
  if (!index)
  {
    int lastSlash = filename.lastIndexOf('/');
    if (lastSlash != -1)
    {
      file = filename.substring(lastSlash + 1);
    }
    lastSlash = filename.lastIndexOf('\\'); // WindowsÂΩ¢Âºè„ÅÆ„Éë„ÇπÂå∫Âàá„Çä„ÇÇËÄÉÊÖÆ
    if (lastSlash != -1)
    {
      file = filename.substring(lastSlash + 1);
    }

    if (!file.startsWith("/"))
      file = "/" + file;

    String fullPath = file;
    if (SdPath != "/")
      fullPath = SdPath + file;

    Serial.println("Upload target filename = " + fullPath);
    request->_tempFile = SD.open(fullPath, "w");

    if (!request->_tempFile)
      Serial.println("Error creating file for upload...");

    SD_uploadSize = 0;
    SD_startTime = millis();
  }

  if (request->_tempFile)
  {
    if (len)
    {
      request->_tempFile.write(data, len);
      // Serial.println("Transferred : " + String(len) + " Bytes");
      SD_uploadSize = SD_uploadSize + len;
    }

    if (final)
    {
      request->_tempFile.close();
      SD_uploadTime = millis() - SD_startTime;
      Serial.println("Upload Complete: " + String(request->_tempFile.name()));
      Serial.println("SD_uploadSize = " + String(SD_uploadSize) + " Bytes");
      Serial.println("SD_uploadTime = " + String(SD_uploadTime) + " mSEC");
      request->redirect("/SD_dir");
    }
  }
}

void SD_Handle_File_Delete(String encoded_filename)
{
  webpage = HTML_Header();
  String decoded_filename = urlDecode(encoded_filename);

  String fullPath = decoded_filename;
  if (!fullPath.startsWith("/"))
    fullPath = "/" + fullPath;

  if (SdPath != "/")
    fullPath = SdPath + fullPath;

  Serial.println("SD Delete execute target filename = " + fullPath + " (decoded)");
  File dataFile = SD.open(fullPath, "r");

  if (dataFile)
  {
    dataFile.close(); // „Éï„Ç°„Ç§„É´„ÇíÈñâ„Åò„Å¶„Åã„ÇâÂâäÈô§
    if (SD.remove(fullPath))
    {
      webpage += "<h3>SD: File '" + decoded_filename + "' in " + SdPath + " has been deleted</h3>";
      webpage += "<a href='/SD_dir'>[OK]</a><br><br>";
    }
    else
    {
      webpage += "<h3>SD: Failed to delete file [ " + decoded_filename + " ] in " + SdPath + "</h3>";
      webpage += "<a href='/SD_delete'>[Back to Select]</a><br><br>";
    }
  }
  else
  {
    webpage += "<h3>SD: File [ " + decoded_filename + " ] in " + SdPath + " does not exist</h3>";
    webpage += "<a href='/SD_delete'>[Back to Select]</a><br><br>";
  }
  webpage += HTML_Footer();
}

void SD_Generate_Confirm_Page(String encoded_filename)
{
  webpage = HTML_Header();
  String decoded_filename = urlDecode(encoded_filename);

  webpage += "<h3>Confirm File Deletion (SD)</h3>";
  webpage += "<p>Are you sure you want to delete the file:</p>";
  webpage += "<p style='font-weight: bold; color: red;'>" + decoded_filename + "</p>";
  // webpage += "<p>in path: " + SdPath + "?</p>";
  webpage += "<p>in path:&nbsp;„Äå&nbsp;" + SdPath + "&nbsp;„Äç&nbsp;?</p>";
  webpage += "<br>";

  // „ÅØ„ÅÑÔºàÂâäÈô§ÂÆüË°åÔºâ„Éú„Çø„É≥ - „Ç®„É≥„Ç≥„Éº„Éâ„Åï„Çå„Åü„Éï„Ç°„Ç§„É´Âêç„ÇíÊ∏°„Åô
  webpage += "<a href='/SD_delete_execute~/" + encoded_filename + "' style='padding: 10px 20px; background-color: #dc3545; color: white; text-decoration: none; border-radius: 5px; margin-right: 10px;'>Yes, Delete</a>";

  // „ÅÑ„ÅÑ„ÅàÔºà„Ç≠„É£„É≥„Çª„É´Ôºâ„Éú„Çø„É≥
  webpage += "<a href='/SD_delete' style='padding: 10px 20px; background-color: #6c757d; color: white; text-decoration: none; border-radius: 5px;'>No, Cancel</a>";

  webpage += HTML_Footer();
}

void SD_File_Rename()
{
  SD_Directory();
  webpage = HTML_Header();
  webpage += "<h3>SD: Select a Dir/File to [RENAME] (" + SdPath + ")</h3>";
  // method„ÇíGET„Å´Â§âÊõ¥„Åó„ÄÅaction„Çí‰øÆÊ≠£ (ÂÖÉ„ÅÆ„Ç≥„Éº„Éâ„Å´Âêà„Çè„Åõ„Çã)
  webpage += "<form action='/SD_renamehandler' method='GET'>";
  webpage += "<table class='file-list-table rename-table'>"; // „ÉÜ„Éº„Éñ„É´ÈñãÂßã

  // --- thead („Éò„ÉÉ„ÉÄ„Éº) ---
  webpage += "<thead>";
  webpage += "<tr>";
  // „Éò„ÉÉ„ÉÄ„Éº„Çí2Âàó„Å´Â§âÊõ¥
  webpage += "<th class='rename-select-header'>Select / File Name</th>";
  webpage += "<th class='rename-new-header'>New Filename</th>";
  webpage += "</tr>";
  webpage += "</thead>";

  // --- tbody („Éú„Éá„Ç£) ---
  webpage += "<tbody>";
  if (SD_numfiles > 0)
  {
    for (int index = 0; index < SD_numfiles; index++)
    {
      String current_filename = SD_Filenames[index].filename;
      String current_ftype = SD_Filenames[index].ftype;
      String radio_id = "choice_" + String(index); // „É©„Ç∏„Ç™„Éú„Çø„É≥Áî®„ÅÆ‰∏ÄÊÑè„Å™ID

      webpage += "<tr class='file-entry'>"; // ÂêÑË°å

      // --- 1ÂàóÁõÆ: Select / File Name ---
      webpage += "<td class='rename-select-cell'>";
      // ÈùûË°®Á§∫„ÅÆ„É©„Ç∏„Ç™„Éú„Çø„É≥: name='choice', value=„Éï„Ç°„Ç§„É´Âêç
      webpage += "<input type='radio' name='choice' value='" + current_filename + "' id='" + radio_id + "' style='display: none;'>";
      // Èö†„Åó„Éï„Ç£„Éº„É´„Éâ: name='oldfile', value=„Éï„Ç°„Ç§„É´Âêç („Éè„É≥„Éâ„É©„Åß„ÅÆÂèñÂæó„ÇíÂÆπÊòì„Å´„Åô„Çã„Åü„ÇÅ)
      webpage += "<input type='hidden' name='oldfile' value='" + current_filename + "'>";
      // „É©„Éô„É´ („Éú„Çø„É≥È¢®): radio_id„Å´ÂØæÂøú‰ªò„Åë
      webpage += "<label for='" + radio_id + "' class='rename-select-label'>";
      if (current_ftype == "Dir")
      {
        webpage += "<span style='color: #007bff;'>&#128193;</span> "; // „Éï„Ç©„É´„ÉÄ„Ç¢„Ç§„Ç≥„É≥
      }
      webpage += current_filename; // „Éï„Ç°„Ç§„É´ÂêçË°®Á§∫
      webpage += "</label>";
      webpage += "</td>";

      // --- 2ÂàóÁõÆ: New Filename ---
      webpage += "<td class='rename-new-name'>";
      // „ÉÜ„Ç≠„Çπ„ÉàÂÖ•Âäõ: name='newfile' (ÂêÑË°å„ÅßÂêå„ÅònameÂ±ûÊÄß)
      webpage += "<input type='text' name='newfile' style='width: 95%; box-sizing: border-box;'>";
      webpage += "</td>";

      webpage += "</tr>"; // Ë°åÁµÇ‰∫Ü
    }
  }
  else
  {
    // „Éï„Ç°„Ç§„É´„Åå„Å™„ÅÑÂ†¥Âêà„ÅÆË°®Á§∫ (colspan„Çí2„Å´Â§âÊõ¥)
    webpage += "<tr><td colspan='2' style='text-align: center; padding: 20px;'>No files or directories found in " + SdPath + " to rename.</td></tr>";
  }
  webpage += "</tbody>";
  webpage += "</table><br>";

  // ÈÄÅ‰ø°„Éú„Çø„É≥
  webpage += "<input type='submit' value='Rename Selected'>";
  webpage += "</form>";
  webpage += HTML_Footer();
}

void SD_Handle_File_Rename(AsyncWebServerRequest *request, String filename, int Args)
{
  String oldfilename_form = "";
  String newfilename_form = "";
  webpage = HTML_Header();

  // 1. ÈÅ∏Êäû„Åï„Çå„Åü„Éï„Ç°„Ç§„É´Âêç (choice „ÅÆ value) „ÇíÂèñÂæó
  if (request->hasParam("choice"))
  {
    oldfilename_form = request->arg("choice");
  }
  else
  {
    // choice „ÅåÈÅ∏Êäû„Åï„Çå„Å¶„ÅÑ„Å™„ÅÑÂ†¥Âêà„ÅÆ„Ç®„É©„ÉºÂá¶ÁêÜ
    webpage += "<h3>SD: Rename Error - No file selected.</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    request->send(200, "text/html", webpage); // „Ç®„É©„Éº„Éö„Éº„Ç∏„ÇíÈÄÅ‰ø°„Åó„Å¶ÁµÇ‰∫Ü
    return;
  }

  // 2. ÂØæÂøú„Åô„Çã newfile „ÇíÂèñÂæó
  //    „Éï„Ç©„Éº„É†„ÅÆÂºïÊï∞„Çí„É´„Éº„Éó„Åó„Å¶„ÄÅÈÅ∏Êäû„Åï„Çå„Åü oldfile „Å´ÂØæÂøú„Åô„Çã newfile „ÇíÊé¢„Åô
  bool found_newfile = false;
  String current_oldfile_check = ""; // „É´„Éº„ÉóÂÜÖ„Åß oldfile „ÇíËøΩË∑°
  for (int i = 0; i < Args; i++)
  {
    String argName = request->argName(i);
    String argValue = request->arg(i);

    if (argName == "oldfile")
    {
      current_oldfile_check = argValue; // ÁèæÂú®„ÅÆË°å„ÅÆ oldfile „ÇíË®òÈå≤
    }
    else if (argName == "newfile" && current_oldfile_check == oldfilename_form)
    {
      // oldfile „ÅåÈÅ∏Êäû„Åï„Çå„Åü„ÇÇ„ÅÆ„Å®‰∏ÄËá¥„Åó„ÄÅ„Åã„Å§ÂºïÊï∞Âêç„Åå newfile „Å™„Çâ„ÄÅ„Åù„Çå„ÅåÂØæÂøú„Åô„ÇãÊñ∞„Åó„ÅÑÂêçÂâç
      newfilename_form = argValue;
      found_newfile = true;
      break; // Ë¶ã„Å§„Åã„Å£„Åü„Çâ„É´„Éº„Éó„ÇíÊäú„Åë„Çã
    }
    // choice „Éë„É©„É°„Éº„Çø„ÅØ„Åì„Åì„Åß„ÅØÁÑ°Ë¶ñ (Êó¢„Å´ÂèñÂæóÊ∏à„Åø„ÅÆ„Åü„ÇÅ)
  }

  // newfile „ÅåË¶ã„Å§„Åã„Çâ„Å™„Åã„Å£„ÅüÂ†¥Âêà (ÈÄöÂ∏∏„ÅØÁô∫Áîü„Åó„Å™„ÅÑ„ÅØ„Åö)
  if (!found_newfile)
  {
    Serial.println("Error: Could not find corresponding newfile parameter for selected oldfile: " + oldfilename_form);
    webpage += "<h3>SD: Rename Error - Internal error processing form data.</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    request->send(200, "text/html", webpage); // „Ç®„É©„Éº„Éö„Éº„Ç∏„ÇíÈÄÅ‰ø°„Åó„Å¶ÁµÇ‰∫Ü
    return;
  }

  Serial.println("Rename requested:");
  Serial.println("  Old filename (from choice): " + oldfilename_form);
  Serial.println("  New filename (from form): " + newfilename_form);
  Serial.println("  Current Path (SdPath): " + SdPath);

  // --- 3. ‰ª•Èôç„ÅÆÂÖ•Âäõ„ÉÅ„Çß„ÉÉ„ÇØ„Å®„É™„Éç„Éº„É†Âá¶ÁêÜ ---
  // ÂÖ•Âäõ„ÉÅ„Çß„ÉÉ„ÇØ (newfilename „ÅåÁ©∫„Åß„Å™„ÅÑ„Åã„ÇÇ„ÉÅ„Çß„ÉÉ„ÇØ)
  if (oldfilename_form == "" || newfilename_form == "") // newfilename„ÇÇ„ÉÅ„Çß„ÉÉ„ÇØ
  {
    webpage += "<h3>SD: Rename Error - File selected but new name is missing.</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    request->send(200, "text/html", webpage); // ‚òÖ send „ÇíËøΩÂä†
    return;
  }
  if (newfilename_form.indexOf('/') != -1 || newfilename_form.indexOf('\\') != -1)
  {
    webpage += "<h3>SD: Rename Error - New filename cannot contain '/' or '\'.</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    request->send(200, "text/html", webpage); // ‚òÖ send „ÇíËøΩÂä†
    return;
  }
  if (oldfilename_form == newfilename_form)
  {
    webpage += "<h3>SD: Rename Error - New filename is the same as the old filename.</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    request->send(200, "text/html", webpage); // ‚òÖ send „ÇíËøΩÂä†
    return;
  }

  // „Éï„É´„Éë„Çπ„ÅÆÊßãÁØâ
  String oldfilepath = oldfilename_form;
  if (!oldfilepath.startsWith("/"))
    oldfilepath = "/" + oldfilepath;
  if (SdPath != "/")
    oldfilepath = SdPath + oldfilepath;

  String newfilepath = newfilename_form;
  if (!newfilepath.startsWith("/"))
    newfilepath = "/" + newfilepath;
  if (SdPath != "/")
    newfilepath = SdPath + newfilepath;

  Serial.println("  Old full path: " + oldfilepath);
  Serial.println("  New full path: " + newfilepath);

  // „Éï„Ç°„Ç§„É´/„Éá„Ç£„É¨„ÇØ„Éà„É™„ÅÆÂ≠òÂú®Á¢∫Ë™ç„Å®„É™„Éç„Éº„É†ÂÆüË°å
  File currentItem = SD.open(oldfilepath, "r");

  if (currentItem)
  {
    currentItem.close();
    if (SD.exists(newfilepath))
    {
      webpage += "<h3>SD: Rename Error - New filename '" + newfilename_form + "' already exists in " + SdPath + ".</h3>";
      webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
    }
    else
    {
      if (SD.rename(oldfilepath, newfilepath))
      {
        webpage += "<h3>SD: Item '" + oldfilename_form + "' in " + SdPath + " has been renamed to '" + newfilename_form + "'</h3>";
        webpage += "<a href='/SD_dir'>[OK]</a><br><br>";
      }
      else
      {
        webpage += "<h3>SD: Rename Error - Failed to rename '" + oldfilename_form + "' to '" + newfilename_form + "' in " + SdPath + ".</h3>";
        webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
      }
    }
  }
  else
  {
    webpage += "<h3>SD: Rename Error - Original item '" + oldfilename_form + "' not found in " + SdPath + ".</h3>";
    webpage += "<a href='/SD_rename'>[Back]</a><br><br>";
  }

  webpage += HTML_Footer();
}

bool SD_notFound(AsyncWebServerRequest *request)
{
  String filename_encoded = "";
  String url = request->url();

  // „Éï„Ç°„Ç§„É´ÂêçÈÉ®ÂàÜ„ÅÆÊäΩÂá∫ ('~/' „ÅÆÂæå) - ‚òÖ„Ç®„É≥„Ç≥„Éº„Éâ„Åï„Çå„Åü„Åæ„ÅæÂèñÂæó
  int separatorIndex = url.indexOf("~/");
  if (separatorIndex != -1)
  {
    filename_encoded = url.substring(separatorIndex + 2);
  }

  // „Éè„É≥„Éâ„É©ÂàÜÂ≤ê
  if (url.startsWith("/SD_downloadhandler"))
  {
    String dl_filename_decoded = urlDecode(filename_encoded);
    Serial.println("SD_Download handler started for: " + dl_filename_decoded + " (decoded)");
    SD_startTime = millis();

    String fullPath = dl_filename_decoded;
    if (!fullPath.startsWith("/"))
      fullPath = "/" + fullPath;
    if (SdPath != "/")
      fullPath = SdPath + fullPath;

    Serial.println("Download target full path = " + fullPath);
    File file = SD.open(fullPath, "r");

    if (file && !file.isDirectory())
    {
      String contentType = getContentType("download");
      AsyncWebServerResponse *response = request->beginResponse(
          contentType,
          file.size(),
          [file](uint8_t *buffer, size_t maxLen, size_t index) mutable -> size_t
          {
            size_t bytesRead = file.read(buffer, maxLen);
            return bytesRead;
          });
      response->setContentLength(file.size());
      response->addHeader("Content-Disposition", "attachment; filename=\"" + dl_filename_decoded + "\"");
      response->addHeader("Server", "ESP Async Web Server");
      request->send(response);

      SD_downloadSize = file.size();
      SD_downloadTime = millis() - SD_startTime;
      Serial.println("SD download handler initiated...");
    }
    else
    {
      if (file)
        file.close();
      Serial.println("Error: File not found or is a directory: " + fullPath);
      request->send(404, "text/plain", "File not found or is a directory");
    }
    return true;
  }
  else if (url.startsWith("/SD_streamhandler"))
  {
    String stream_filename_decoded = urlDecode(filename_encoded);
    Serial.println("SD_Stream handler started for: " + stream_filename_decoded + " (decoded)");
    SD_startTime = millis();

    String fullPath = stream_filename_decoded;
    if (!fullPath.startsWith("/"))
      fullPath = "/" + fullPath;
    if (SdPath != "/")
      fullPath = SdPath + fullPath;

    Serial.println("Stream target full path = " + fullPath);

    if (SD.exists(fullPath))
    {
      File file = SD.open(fullPath, "r");
      if (file && !file.isDirectory())
      {
        String contentType = getContentType(stream_filename_decoded);
        AsyncWebServerResponse *response = request->beginResponse(SD, fullPath, contentType);
        SD_downloadSize = file.size();
        file.close();
        SD_downloadTime = millis() - SD_startTime;
        request->send(response);
        Serial.println("SD stream handler initiated...");
      }
      else
      {
        if (file)
          file.close();
        Serial.println("Error: Cannot stream directory or file open failed: " + fullPath);
        request->send(404, "text/plain", "Cannot stream directory or file open failed");
      }
    }
    else
    {
      Serial.println("Error: File not found for streaming: " + fullPath);
      request->send(404, "text/plain", "File not found for streaming");
    }
    return true;
  }
  else if (url.startsWith("/SD_delete_confirm"))
  {
    Serial.println("SD_Delete confirm page requested for: " + filename_encoded);
    SD_Generate_Confirm_Page(filename_encoded);
    request->send(200, "text/html", webpage);
    return true;
  }
  else if (url.startsWith("/SD_delete_execute"))
  {
    Serial.println("SD_Delete execute handler started for: " + filename_encoded);
    SD_Handle_File_Delete(filename_encoded);
    request->send(200, "text/html", webpage);
    return true;
  }
  else if (url.startsWith("/SD_renamehandler"))
  {
    Serial.println("SD Rename handler started...");
    SD_Handle_File_Rename(request, "", request->args());
    request->send(200, "text/html", webpage);
    return true;
  }
  return false;
}

void SD_Select_File_For_Function(String title, String function)
{
  SDdir_FilesList();

  webpage = HTML_Header();
  webpage += "<h3>SD: Select a File to " + title + " (" + SdPath + ")</h3>";

  if (SD_numfiles > 0)
  {
    webpage += "<table class='file-list-table'>";
    webpage += "<tbody>";

    for (int index = 0; index < SD_numfiles; index++)
    {
      String Fname_orig = SD_Filenames[index].filename;
      String Fname_encoded = urlEncode(Fname_orig);

      String target_function = function;
      if (function == "SD_deletehandler")
      {
        target_function = "SD_delete_confirm";
      }

      webpage += "<tr class='file-entry'>";
      webpage += "<td class='file-name'>";
      webpage += "<button style='width: 100%; text-align: left; padding: 5px; box-sizing: border-box; white-space: normal; word-break: break-all;'>";

      webpage += "<a href='" + target_function + "~/" + Fname_encoded + "' style='display: block; text-decoration: none; color: inherit;'>" + Fname_orig + "</a>";
      webpage += "</button>";
      webpage += "</td>";
      webpage += "<td class='file-size'>" + SD_Filenames[index].fsize + "</td>";
      webpage += "</tr>";
    }
    webpage += "</tbody>";
    webpage += "</table>";
  }
  else
  {
    webpage += "<p style='text-align: center; margin-top: 20px;'>No files found in " + SdPath + " to " + title + "</p>";
  }
  webpage += HTML_Footer();
}

void SD_Select_File_For_ViewText()
{
  SDdir_FilesList();

  webpage = HTML_Header();
  webpage += "<h3>SD: Select a Text File to View (" + SdPath + ")</h3>";

  // Ë®±ÂèØ„Åô„ÇãÊã°ÂºµÂ≠ê„ÅÆ„É™„Çπ„Éà (Â∞èÊñáÂ≠ó„ÅßÂÆöÁæ©)
  const std::vector<String> allowedExtensions = {
      ".txt", ".log", ".csv", ".json", ".yaml", ".htm", ".html", ".css", ".js", ".xml", ".md", ".ini", ".conf", ".cfg", ".c", ".h", ".cpp", ".hpp", ".py", ".inc"
      // ÂøÖË¶Å„Å´Âøú„Åò„Å¶‰ªñ„ÅÆ„ÉÜ„Ç≠„Çπ„Éà„Éô„Éº„Çπ„ÅÆÊã°ÂºµÂ≠ê„ÇíËøΩÂä†„Åô„Çã
  };

  int displayedFileCount = 0; // Ë°®Á§∫„Åó„Åü„Éï„Ç°„Ç§„É´„ÅÆÊï∞„Çí„Ç´„Ç¶„É≥„Éà

  if (SD_numfiles > 0) // SD_numfiles „ÅØ SDdir_FilesList() „ÅßË®≠ÂÆö„Åï„Çå„Åü„Éï„Ç°„Ç§„É´Á∑èÊï∞
  {
    webpage += "<table class='file-list-table'>";
    webpage += "<tbody>";

    // ÂèñÂæó„Åó„ÅüÂÖ®„Éï„Ç°„Ç§„É´„Çí„É´„Éº„Éó
    for (int index = 0; index < SD_numfiles; index++)
    {
      String Fname_orig = SD_Filenames[index].filename;
      String Fname_lower = Fname_orig; // ÊØîËºÉÁî®„Å´„Éï„Ç°„Ç§„É´Âêç„ÇíÂ∞èÊñáÂ≠ó„Å´Â§âÊèõ
      Fname_lower.toLowerCase();

      bool isAllowed = false; // „Åì„ÅÆ„Éï„Ç°„Ç§„É´„ÇíË°®Á§∫„Åô„Çã„Åã„Å©„ÅÜ„Åã„ÅÆ„Éï„É©„Ç∞

      // Ë®±ÂèØ„É™„Çπ„ÉàÂÜÖ„ÅÆÊã°ÂºµÂ≠ê„Å®‰∏ÄËá¥„Åô„Çã„Åã„ÉÅ„Çß„ÉÉ„ÇØ
      for (const String &ext : allowedExtensions)
      {
        if (Fname_lower.endsWith(ext))
        {
          isAllowed = true; // ‰∏ÄËá¥„Åó„Åü„Çâ„Éï„É©„Ç∞„ÇíÁ´ã„Å¶„Å¶„É´„Éº„Éó„ÇíÊäú„Åë„Çã
          break;
        }
      }

      // Ë®±ÂèØ„Åï„Çå„ÅüÊã°ÂºµÂ≠ê„ÅÆÂ†¥Âêà„ÅÆ„Åø„ÄÅ„ÉÜ„Éº„Éñ„É´„Å´Ë°å„ÇíËøΩÂä†
      if (isAllowed)
      {
        displayedFileCount++;
        String Fname_encoded = urlEncode(Fname_orig);
        String link_url = "/SD_view_text?file=" + Fname_encoded;

        webpage += "<tr class='file-entry'>";
        webpage += "<td class='file-name'>";
        webpage += "<button style='width: 100%; text-align: left; padding: 5px; box-sizing: border-box; white-space: normal; word-break: break-all;'>";
        webpage += "<a href='" + link_url + "' style='display: block; text-decoration: none; color: inherit;'>" + Fname_orig + "</a>";
        webpage += "</button>";
        webpage += "</td>";
        webpage += "<td class='file-size'>" + SD_Filenames[index].fsize + "</td>";
        webpage += "</tr>";
      }
    }

    webpage += "</tbody>";
    webpage += "</table>";
  }

  // Ë°®Á§∫„Åô„Çã„ÉÜ„Ç≠„Çπ„Éà„Éï„Ç°„Ç§„É´„Åå‰∏Ä„Å§„ÇÇ„Å™„Åã„Å£„ÅüÂ†¥Âêà„ÅÆ„É°„ÉÉ„Çª„Éº„Ç∏
  // SD_numfiles > 0 „Åß„ÇÇ„ÄÅË®±ÂèØ„Åï„Çå„ÅüÊã°ÂºµÂ≠ê„ÅÆ„Éï„Ç°„Ç§„É´„Åå„Å™„Åë„Çå„Å∞ displayedFileCount „ÅØ 0 „ÅÆ„Åæ„Åæ
  if (displayedFileCount == 0)
  {
    // ÂÖÉ„ÄÖ„Éï„Ç°„Ç§„É´„Åå„Å™„Åã„Å£„ÅüÂ†¥Âêà„ÇÇ„ÄÅ„ÉÜ„Ç≠„Çπ„Éà„Éï„Ç°„Ç§„É´„Åå„Å™„Åã„Å£„ÅüÂ†¥Âêà„ÇÇ„Åì„ÅÆ„É°„ÉÉ„Çª„Éº„Ç∏„ÇíË°®Á§∫
    webpage += "<p style='text-align: center; margin-top: 20px;'>No text files found in " + SdPath + "</p>";
  }

  webpage += HTML_Footer();
}

void SD_View_Text(AsyncWebServerRequest *request, String encoded_filename)
{
  String decoded_filename = urlDecode(encoded_filename);
  String fullPath = decoded_filename;
  if (!fullPath.startsWith("/"))
    fullPath = "/" + fullPath;
  if (SdPath != "/")
    fullPath = SdPath + fullPath;

  Serial.println("Viewing text file: " + fullPath);

  File file = SD.open(fullPath, "r");
  if (!file || file.isDirectory())
  {
    if (file)
      file.close();
    webpage = HTML_Header();
    webpage += "<h3>Error: Cannot view file</h3>";
    webpage += "<p>File not found or is a directory: " + decoded_filename + "</p>";
    webpage += "<a href='/SD_dir'>[Back to Directory]</a>";
    webpage += HTML_Footer();
    request->send(404, "text/html", webpage);
    return;
  }

  webpage = HTML_Header();
  webpage += "<h3>Viewing Text: " + decoded_filename + " (" + SdPath + ")</h3>";
  webpage += "<pre>"; // pre„Çø„Ç∞„ÅßÂõ≤„ÇÄ

  // „Éï„Ç°„Ç§„É´ÂÜÖÂÆπ„ÇíË™≠„ÅøËæº„Çì„Åß webpage „Å´ËøΩÂä†
  // Â§ß„Åç„Å™„Éï„Ç°„Ç§„É´„ÅÆÂ†¥Âêà„ÄÅ„É°„É¢„É™„Å´Ê≥®ÊÑè„ÅåÂøÖË¶Å„ÄÇ
  // „Åì„Åì„Åß„ÅØ‰∏ÄÊã¨Ë™≠„ÅøËæº„Åø„Çí„Åó„Å¶„ÅÑ„Çã„ÄÇ
  while (file.available())
  {
    // ‰∏ÄË°å„Åö„Å§Ë™≠„ÅøËæº„ÇÄ„Åã„ÄÅ„Éê„ÉÉ„Éï„Ç°„ÅßË™≠„ÅøËæº„ÇÄ
    String line = file.readStringUntil('\n');
    // HTML„Ç®„Çπ„Ç±„Éº„Éó„ÅåÂøÖË¶Å„Å™Â†¥Âêà (‰æã: < > & „ÇíË°®Á§∫„Åó„Åü„ÅÑÂ†¥Âêà)
    line.replace("&", "&amp;");
    line.replace("<", "&lt;");
    line.replace(">", "&gt;");
    webpage += line + "\n"; // ÊîπË°å„ÇÇÁ∂≠ÊåÅ
  }
  file.close();

  webpage += "</pre>";
  webpage += "<br><a href='/SD_dir'>[Back to Directory]</a>";
  webpage += "<br>";
  webpage += HTML_Footer();
  request->send(200, "text/html", webpage);
}

// -------------------------------------------------------
// SDdir_* Èñ¢Êï∞Áæ§
// -------------------------------------------------------
void SDdir_flserverSetup()
{
  server.on("/SDdir_chdir", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SDdir_chdir...");
    SDdir_Select_Dir_For_Function("[CHDIR:change directory]", "SDdir_chdirhandler");
    request->send(200, "text/html", webpage); });

  server.on("/SDdir_mkdir", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SDdir_mkdir ...");
    SDdir_DirMake();
    request->send(200, "text/html", webpage); });

  // GET„ÅßÂèó„Åë„ÇãÂ†¥Âêà (SDdir_InputNewDirName „Åã„Çâ„ÅÆÈÅ∑Áßª)
  server.on("/SDdir_mkdirhandler", HTTP_GET, [](AsyncWebServerRequest *request)
            {
        Serial.println("SDdir_mkdir handler (GET) started...");
        SDdir_Handle_mkdir(request);
        request->send(200, "text/html", webpage); });

  server.on("/SDdir_rmdir", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("SDdir_rmdir...");
    SDdir_Select_Dir_For_Function("[RMDIR:remove directory]", "SDdir_rmdirhandler");
    request->send(200, "text/html", webpage); });

  server.on("/SDdir_chTop", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("change SdPath to Root directory...");
    SDdir_handle_chTop();
    request->send(200, "text/html", webpage); });

  server.on("/SDdir_chUp", HTTP_GET, [](AsyncWebServerRequest *request)
            {
    Serial.println("change SdPath to Up directory...");
    SDdir_handle_chUp();
    request->send(200, "text/html", webpage); });
}

void SDdir_handle_chTop()
{
  SdPath = String("/");
  webpage = HTML_Header();
  webpage += "<h3>Changed SD Path to Root Directory (/)</h3>";
  webpage += "<a href='/SD_dir'>[Show Content]</a><br><br>";
  webpage += HTML_Footer();
}

void SDdir_handle_chUp()
{
  String upPath = String("/");

  if (SdPath != "/")
  {
    int i = SdPath.lastIndexOf('/');
    if (i > 0)
    { // „É´„Éº„Éà("/")„ÅÆ‰∏Ä„Å§‰∏ã„ÅÆÈöéÂ±§„ÅÆÂ†¥Âêà„ÄÅi„ÅØ0„Å´„Å™„Çã
      upPath = SdPath.substring(0, i);
    }
    else
    {
      // „É´„Éº„ÉàÁõ¥‰∏ã„Åã„ÄÅ‰∫àÊúü„Åõ„Å¨„Éë„ÇπÂΩ¢Âºè„ÅÆÂ†¥Âêà„ÅØ„É´„Éº„Éà„Å´Êàª„Åô
      upPath = "/";
    }
  }
  // upPath „Åå "/" „ÅÆÂ†¥Âêà„ÅØÂ§âÊõ¥„Åó„Å™„ÅÑ
  if (SdPath != "/")
  {
    SdPath = upPath;
  }

  Serial.println("Changed SdPath to = " + SdPath);
  webpage = HTML_Header();
  webpage += "<h3>Changed SD Path to Up Directory (" + SdPath + ")</h3>";
  webpage += "<a href='/SD_dir'>[Show Content]</a><br><br>";
  webpage += HTML_Footer();
}

void SDdir_Handle_chdir(String encoded_filename)
{
  String filename = urlDecode(encoded_filename);

  webpage = HTML_Header();
  String targetPath = filename;

  if (!targetPath.startsWith("/"))
    targetPath = "/" + targetPath;

  if (SdPath != "/")
    targetPath = SdPath + targetPath;

  Serial.println("Change directory target = " + targetPath);

  // „Çø„Éº„Ç≤„ÉÉ„Éà„Éë„Çπ„ÅÆÊ≠£Ë¶èÂåñ (‰æã: "/dir1//dir2" -> "/dir1/dir2")
  targetPath.replace("//", "/");
  if (targetPath != "/" && targetPath.endsWith("/"))
  {
    targetPath = targetPath.substring(0, targetPath.length() - 1);
  }

  File dir = SD.open(targetPath);
  if (dir && dir.isDirectory())
  { // „Éá„Ç£„É¨„ÇØ„Éà„É™„ÅåÂ≠òÂú®„Åô„Çã„ÅãÁ¢∫Ë™ç
    dir.close();
    SdPath = targetPath;
    Serial.println("Successfully changed SdPath = " + SdPath);
    webpage += "<h3>Directory changed to '" + SdPath + "'</h3>";
    webpage += "<a href='/SD_dir'>[Show Content]</a><br><br>";
  }
  else
  {
    if (dir)
      dir.close(); // „Éï„Ç°„Ç§„É´„Å†„Å£„ÅüÂ†¥Âêà„ÇÑÈñã„Åë„Å™„Åã„Å£„ÅüÂ†¥Âêà„Å´Èñâ„Åò„Çã
    Serial.println("Failed to change directory to [ " + targetPath + " ]");
    webpage += "<h3>Error: Directory [ " + filename + " ] not found or is not a directory in " + SdPath + "</h3>";
    webpage += "<a href='/SDdir_chdir'>[Back to Select]</a><br><br>"; // ÈÅ∏ÊäûÁîªÈù¢„Å´Êàª„Çã„É™„É≥„ÇØ
    webpage += "<a href='/SD_dir'>[Show Current Content]</a><br><br>";
  }
  webpage += HTML_Footer();
}

void SDdir_Handle_rmdir(String encoded_filename)
{
  String filename = urlDecode(encoded_filename);
  webpage = HTML_Header();
  String targetPath = filename;

  if (!targetPath.startsWith("/"))
    targetPath = "/" + targetPath;

  if (SdPath != "/")
    targetPath = SdPath + targetPath;

  Serial.println("Remove directory target = " + targetPath);

  // „É´„Éº„Éà„Éá„Ç£„É¨„ÇØ„Éà„É™„ÅØÂâäÈô§‰∏çÂèØ
  if (targetPath == "/")
  {
    webpage += "<h3>Error: Cannot remove the root directory.</h3>";
    webpage += "<a href='/SDdir_rmdir'>[Back to Select]</a><br><br>";
    webpage += HTML_Footer();
    return;
  }

  // „Éá„Ç£„É¨„ÇØ„Éà„É™ÂâäÈô§ÂÆüË°å
  if (SD.rmdir(targetPath))
  {
    Serial.println("Removed dir = " + targetPath);
    webpage += "<h3>Directory '" + filename + "' in " + SdPath + " has been removed</h3>";
    webpage += "<a href='/SD_dir'>[OK]</a><br><br>"; // ÊàêÂäü„Åó„Åü„ÇâDirË°®Á§∫„Å∏
  }
  else
  {
    // Â§±ÊïóÁêÜÁî±„ÇíÊé®Ê∏¨ (Â≠òÂú®„Åó„Å™„ÅÑ„ÄÅÁ©∫„Åß„Å™„ÅÑ„ÄÅ„Å™„Å©)
    String errorMsg = "<h3>Error: Failed to remove directory [ " + filename + " ] in " + SdPath + ".";
    File dir = SD.open(targetPath);
    if (!dir)
    {
      errorMsg += " (Directory not found)";
    }
    else
    {
      dir.close();
      // Á©∫„Åã„Å©„ÅÜ„Åã„Çí„ÉÅ„Çß„ÉÉ„ÇØ„Åô„Çã„ÅÆ„ÅØÈõ£„Åó„ÅÑÂ†¥Âêà„Åå„ÅÇ„Çã
      errorMsg += " (Directory might not be empty or is write-protected)";
    }
    errorMsg += "</h3>";
    webpage += errorMsg;
    webpage += "<a href='/SDdir_rmdir'>[Back to Select]</a><br><br>";
  }
  webpage += HTML_Footer();
}

void SDdir_Handle_mkdir(AsyncWebServerRequest *request)
{
  webpage = HTML_Header();
  String filename = "";

  // GET „Åæ„Åü„ÅØ POST „Éë„É©„É°„Éº„Çø„Åã„Çâ„Éï„Ç°„Ç§„É´Âêç„ÇíÂèñÂæó
  if (request->hasParam("filename", false))
  { // GET „Éë„É©„É°„Éº„Çø„ÉÅ„Çß„ÉÉ„ÇØ
    filename = request->arg("filename");
  }
  else if (request->hasParam("filename", true))
  { // POST „Éë„É©„É°„Éº„Çø„ÉÅ„Çß„ÉÉ„ÇØ
    filename = request->getParam("filename", true)->value();
  }

  Serial.println("Make directory requested name = " + filename);

  // ÂÖ•Âäõ„ÉÅ„Çß„ÉÉ„ÇØ
  if (filename == "")
  {
    webpage += "<h3>Error: Directory name cannot be empty.</h3>";
    webpage += "<a href='/SDdir_mkdir'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    return;
  }
  if (filename.indexOf('/') != -1 || filename.indexOf('\\') != -1)
  {
    webpage += "<h3>Error: Directory name cannot contain '/' or '\'.</h3>";
    webpage += "<a href='/SDdir_mkdir'>[Back]</a><br><br>";
    webpage += HTML_Footer();
    return;
  }

  // „Éï„É´„Éë„ÇπÊßãÁØâ
  String fullPath = filename;
  if (!fullPath.startsWith("/"))
    fullPath = "/" + fullPath;

  if (SdPath != "/")
    fullPath = SdPath + fullPath;

  Serial.println("Make directory full path = " + fullPath);

  // „Éá„Ç£„É¨„ÇØ„Éà„É™‰ΩúÊàêÂÆüË°å
  if (SD.mkdir(fullPath))
  {
    Serial.println("Created new dir = " + fullPath);
    webpage += "<h3>Directory '" + filename + "' has been created in " + SdPath + "</h3>";
    webpage += "<a href='/SD_dir'>[OK]</a><br><br>"; // ÊàêÂäü„Åó„Åü„ÇâDirË°®Á§∫„Å∏
  }
  else
  {
    String errorMsg = "<h3>Error: Failed to create directory [ " + filename + " ] in " + SdPath + ".";
    if (SD.exists(fullPath))
    {
      errorMsg += " (Item with the same name already exists)";
    }
    else
    {
      errorMsg += " (Invalid path or insufficient permissions)";
    }
    errorMsg += "</h3>";
    webpage += errorMsg;
    webpage += "<a href='/SDdir_mkdir'>[Back]</a><br><br>";
  }
  webpage += HTML_Footer();
}

void SDdir_DirMake()
{
  SDdir_InputNewDirName("Make New Directory in " + SdPath, "SDdir_mkdirhandler", "filename");
}

void SDdir_Select_Dir_For_Function(String title, String function)
{
  SDdir_DirList();
  webpage = HTML_Header();
  webpage += "<h3>SD: Select a Directory to " + title + " (" + SdPath + ")</h3>";

  if (SD_numfiles > 0)
  {
    webpage += "<table class='file-list-table'>";
    webpage += "<tbody>";

    for (int index = 0; index < SD_numfiles; index++)
    {
      String Dname_orig = SD_Filenames[index].filename;
      String Dname_encoded = urlEncode(Dname_orig);

      String target_function = function;
      if (function == "SDdir_rmdirhandler")
      {
        target_function = "SDdir_rmdir_confirm";
      }

      webpage += "<tr class='file-entry'>";
      webpage += "<td class='file-name'>";
      webpage += "<button style='width: 100%; text-align: left; padding: 5px; box-sizing: border-box; white-space: normal; word-break: break-all;'>";
      webpage += "<a href='" + target_function + "~/" + Dname_encoded + "' style='display: block; text-decoration: none; color: inherit;'>";
      webpage += "<span style='color: #007bff;'>&#128193;</span> " + Dname_orig;
      webpage += "</a>";
      webpage += "</button>";
      webpage += "</td>";
      webpage += "</tr>";
    }
    webpage += "</tbody>";
    webpage += "</table>";
  }
  else
  {
    webpage += "<p style='text-align: center; margin-top: 20px;'>No sub-directories found in " + SdPath + "</p>";
  }

  webpage += HTML_Footer();
}

void SDdir_Generate_Confirm_Page(String encoded_filename)
{
  webpage = HTML_Header();
  String decoded_filename = urlDecode(encoded_filename);

  webpage += "<h3>Confirm Directory Deletion</h3>";
  webpage += "<p>Are you sure you want to delete the directory:</p>";
  webpage += "<p style='font-weight: bold; color: red;'>" + decoded_filename + "</p>";
  // webpage += "<p>in path: " + SdPath + "?</p>";
  webpage += "<p>in path:&nbsp;„Äå&nbsp;" + SdPath + "&nbsp;„Äç&nbsp;?</p>";
  webpage += "<p style='color: grey;'>Note: Only empty directories can be deleted.</p>";
  webpage += "<br>";

  // „ÅØ„ÅÑÔºàÂâäÈô§ÂÆüË°åÔºâ„Éú„Çø„É≥
  webpage += "<a href='/SDdir_rmdir_execute~/" + encoded_filename + "' style='padding: 10px 20px; background-color: #dc3545; color: white; text-decoration: none; border-radius: 5px; margin-right: 10px;'>Yes, Delete</a>";

  // „ÅÑ„ÅÑ„ÅàÔºà„Ç≠„É£„É≥„Çª„É´Ôºâ„Éú„Çø„É≥
  webpage += "<a href='/SDdir_rmdir' style='padding: 10px 20px; background-color: #6c757d; color: white; text-decoration: none; border-radius: 5px;'>No, Cancel</a>";

  webpage += HTML_Footer();
}

void SDdir_DirList()
{ // 'Dir' type only , not involve 'File'
  SD_numfiles = 0;
  SD_Filenames.clear();

  if (SdPath == "")
    SdPath = "/";
  Serial.println("Listing directories in: " + SdPath);
  File root = SD.open(SdPath, "r");

  if (root && root.isDirectory()) // „Éá„Ç£„É¨„ÇØ„Éà„É™„Å®„Åó„Å¶Èñã„Åë„Çã„ÅãÁ¢∫Ë™ç
  {
    root.rewindDirectory();
    File file = root.openNextFile();
    while (file)
    {
      if (file.isDirectory()) // „Éá„Ç£„É¨„ÇØ„Éà„É™„Åã„Å©„ÅÜ„Åã„Çí„ÉÅ„Çß„ÉÉ„ÇØ
      {
        String tmp_filename = file.name(); // „Éï„É´„Éë„Çπ„ÅåËøî„Çã„Åì„Å®„Åå„ÅÇ„Çã
        // „Éë„Çπ„Åã„ÇâÊúÄÂæå„ÅÆÈÉ®ÂàÜÔºà„Éá„Ç£„É¨„ÇØ„Éà„É™ÂêçÔºâ„ÇíÊäΩÂá∫
        int lastSlash = tmp_filename.lastIndexOf('/');
        if (lastSlash != -1)
        {
          tmp_filename = tmp_filename.substring(lastSlash + 1);
        }

        if (tmp_filename != "" && tmp_filename != SD_SYSTEM_FILE) // „Ç∑„Çπ„ÉÜ„É†„Éï„Ç©„É´„ÉÄ„Å®Á©∫Âêç„ÅØÈô§Â§ñ
        {
          fileinfo tmp;
          tmp.filename = tmp_filename;
          tmp.ftype = "Dir";
          tmp.fsize = ""; // „Éá„Ç£„É¨„ÇØ„Éà„É™„Å™„ÅÆ„Åß„Çµ„Ç§„Ç∫„ÅØÁ©∫

          SD_Filenames.push_back(tmp);
          SD_numfiles++;
        }
      }
      file = root.openNextFile();
    }
    root.close();
  }
  else
  {
    if (root)
      root.close(); // Èñã„Åë„Åü„Åå„Éá„Ç£„É¨„ÇØ„Éà„É™„Åß„Å™„Åã„Å£„ÅüÂ†¥Âêà
    Serial.println("Error opening directory: " + SdPath);
  }
  std::sort(SD_Filenames.begin(), SD_Filenames.end(), compareFileinfo); // ÂêçÂâçÈ†Ü„ÇΩ„Éº„Éà
}

void SDdir_FilesList()
{ // 'File' type only , not involve 'Dir'
  SD_numfiles = 0;
  SD_Filenames.clear();
  if (SdPath == "")
    SdPath = "/";
  // Serial.println("Listing files in: " + SdPath);
  File root = SD.open(SdPath, "r");

  if (root && root.isDirectory())
  {
    root.rewindDirectory();
    File file = root.openNextFile();

    while (file)
    {
      if (!file.isDirectory()) // „Éï„Ç°„Ç§„É´„Åã„Å©„ÅÜ„Åã„Çí„ÉÅ„Çß„ÉÉ„ÇØ
      {
        String tmp_filename = file.name();
        int lastSlash = tmp_filename.lastIndexOf('/');
        if (lastSlash != -1)
        {
          tmp_filename = tmp_filename.substring(lastSlash + 1);
        }

        if (tmp_filename != "")
        { // Á©∫„ÅÆ„Éï„Ç°„Ç§„É´Âêç„ÅØÈô§Â§ñ
          fileinfo tmp;
          tmp.filename = tmp_filename;
          tmp.ftype = "File";
          tmp.fsize = ConvBytesUnits(file.size(), 1);

          SD_Filenames.push_back(tmp);
          SD_numfiles++;
        }
      }
      file = root.openNextFile();
    }
    root.close();
  }
  else
  {
    if (root)
      root.close();
    Serial.println("Error opening directory for file listing: " + SdPath);
  }
  std::sort(SD_Filenames.begin(), SD_Filenames.end(), compareFileinfo);
}

bool SDdir_notFound(AsyncWebServerRequest *request)
{
  String filename_encoded = "";
  String url = request->url();

  // „Éï„Ç°„Ç§„É´ÂêçÈÉ®ÂàÜ„ÅÆÊäΩÂá∫ ('~/' „ÅÆÂæå) - ‚òÖ„Ç®„É≥„Ç≥„Éº„Éâ„Åï„Çå„Åü„Åæ„ÅæÂèñÂæó
  int separatorIndex = url.indexOf("~/");
  if (separatorIndex != -1)
  {
    filename_encoded = url.substring(separatorIndex + 2); // „Ç®„É≥„Ç≥„Éº„Éâ„Åï„Çå„Åü„Éï„Ç°„Ç§„É´Âêç
  }

  if (url.startsWith("/SDdir_chdirhandler"))
  {
    Serial.println("SDdir_chdir handler started for: " + filename_encoded);
    SDdir_Handle_chdir(filename_encoded);
    request->send(200, "text/html", webpage);
    return true;
  }
  else if (url.startsWith("/SDdir_mkdirhandler")) // GET„É™„ÇØ„Ç®„Çπ„Éà„ÅßÂá¶ÁêÜ
  {
    Serial.println("SDdir_mkdir handler (from notFound) started...");
    SDdir_Handle_mkdir(request); // request„Ç™„Éñ„Ç∏„Çß„ÇØ„Éà„ÇíÊ∏°„Åó„Å¶‰∏≠„ÅßÂºïÊï∞„ÇíËß£Êûê
    request->send(200, "text/html", webpage);
    return true;
  }
  else if (url.startsWith("/SDdir_rmdir_confirm"))
  {
    Serial.println("SDdir_rmdir confirm page requested for: " + filename_encoded);
    SDdir_Generate_Confirm_Page(filename_encoded);
    request->send(200, "text/html", webpage);
    return true;
  }
  else if (url.startsWith("/SDdir_rmdir_execute"))
  {
    Serial.println("SDdir_rmdir execute handler started for: " + filename_encoded);
    SDdir_Handle_rmdir(filename_encoded);
    request->send(200, "text/html", webpage);
    return true;
  }

  return false;
}

void SDdir_InputNewDirName(String Heading, String Command, String Arg_name)
{
  webpage = HTML_Header();
  webpage += "<h3>" + Heading + "</h3>";
  webpage += "<form method='GET' action='/" + Command + "'>";
  webpage += "New Directory Name: <input type='text' name='" + Arg_name + "' required pattern='[^/\\]+' title='Directory name cannot contain / or \'><br><br>";
  webpage += "<input type='submit' value='Create'>";
  webpage += "</form>";
  webpage += "<br><a href='/SD_dir'>[Cancel and Back to Directory]</a>";
  webpage += HTML_Footer();
}
